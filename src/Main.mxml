<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   initialize="init()" 
					   applicationComplete="applicationCompleted()"
					   showStatusBar="false"
					   >
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import Basic.Api;
			
			import Events.*;
			
			import Views.BookView;
			import Views.ChapterView;
			import Views.CropView;
			import Views.DrawingView;
			import Views.DrawingsView;
			import Views.SyncView;
			
			import com.nocircleno.graffiti.tools.FillBucketTool;
			
			import entities.*;
			import entities.DisplayStackConstants;
			import entities.Library;
			
			import flash.display.Stage;
			import flash.utils.*;
			
			import mx.controls.Alert;
			
			import spark.components.BorderContainer;
			import spark.components.Image;
			import spark.components.Label;
			import spark.skins.spark.ImageSkin;
			
			// variables
			public var display:DisplayStack;
			public var api:Api;
			public var library: Library;
			public var currentBook:String;
			public var currentPage:Number;
			public var currentThumbnails:Array;
			
			public var books: Array;
			public var book: Book;
			public var page: Page;
			public var chapters: Array;
			public var items: Array;

			private var pageResolution: Number = Page._640;
			
			public var pageViewY: Number = 0;
			
			private function applicationCompleted(): void{
				
				stage.displayState = StageDisplayState.FULL_SCREEN_INTERACTIVE;
				
				this.display = new DisplayStack();
				this.addElement(this.display);
				this.library = new Library();
				
				// add event handlers
				//this.addEventListener("book-reading-finished", fillLibrary);
				this.addEventListener(BookClickEvent.BOOK_CLICKED, showBookView);
				this.addEventListener("close clicked", closeApplication);
				this.addEventListener("search-view clicked", showSearchView);
				this.addEventListener("back-to-library", backToLibrary);
				
				this.addEventListener("crop-click", initializeCropView);
				this.addEventListener("show drawings menu", this.showDrawingsMenu);
				this.addEventListener("zoom in click", this.zoomInClick);
				this.addEventListener("zoom out click", this.zoomOutClick);
				this.addEventListener("item-view-close", this.itemViewClose);
				this.addEventListener("item-view-ready", this.display.itemViewReady);
				
				this.addEventListener("sync-clicked", this.openSyncView);
				this.addEventListener("sync-close", this.closeSyncView);
				
				this.addEventListener("error-crop-view", this.errorCropView);
				
				this.addEventListener(PagePaletteClickEvent.LEFT_CLICKED, this.previousPage);
				this.addEventListener(PagePaletteClickEvent.RIGHT_CLICKED, this.nextPage);
				this.addEventListener(PagePaletteClickEvent.CHAPTERS_CLICKED, this.bookChapters);
				
				this.addEventListener(DrawingSaveEvent.DRAWING_SAVE, this.saveDrawing);
				this.addEventListener(DrawingSaveEvent.DRAWING_UPDATE, this.updateDrawing);
				this.addEventListener(DrawingSaveEvent.DRAWING_SHARE, this.shareDrawing);
				
				this.display.FullHeight = this.stage.height;
				this.display.FullWidth = this.stage.width;
				
				// get library
				var self: Object = this;
				setTimeout( function(): void {
					self.library.readBooks();
					self.books = new Array(self.library.getBookCount());
					self.books = self.library.getBooks();
					
					self.display.mainStack.selectedIndex = DisplayStackConstants.LIBRARY_VIEW_INDEX;
					self.addEventListener("library-ready", fillLibrary);
				}, 1000);
			}
			
			// close application
			private function closeApplication(e:Event): void {
				this.exit();
			}
			
			private function showSearchView(e: Event): void {
				this.display.mainStack.selectedIndex = DisplayStackConstants.SEARCH_VIEW_INDEX;
			}
			
			private function showBookView(e: BookClickEvent): void {
				this.display.mainStack.selectedIndex = DisplayStackConstants.PAGE_VIEW_INDEX;
				
				this.page = null;
				this.bindBookPages(e.book);
				if(this.display.tableOfContents == null){
					this.addEventListener("tableofcontents-ready", this.bindBookChapters);
				}else{
					this.bindBookChapters(null);
				}
				
				if(this.display.pageView == null) {
					this.addEventListener("page-view-ready", this.pageViewReady);
				} else {
					this.setFirstPage();
				}
			}			
			
			private function bindBookPages(book: Book): void {
				this.book = book;
			}
			
			private function bindBookChapters(e: Event): void {
				
				this.chapters = this.book.chapters;
				if(this.chapters == null){
					this.chapters = new Array();
				}
				
				this.display.tableOfContents.chapterArea.removeAllElements();
				
				for each(var chapter: Chapter in this.chapters) {
					var label: Label = new spark.components.Label();
					
					label.text = chapter.title.length > 30 ? chapter.title.substr(0, 30) + ' ...' : chapter.title;
					label.setStyle("color", "#FFFFFF");
					label.setStyle("fontSize", 20);
					label.height = 30;
					label.setStyle("paddingTop", 10);
					label.setStyle("paddingLeft", 10);
					label.id = "chapter-" + chapter.ID;
					label.width = this.display.tableOfContents.chapterArea.width;
					
					label.addEventListener(MouseEvent.CLICK, this.chapterClicked);
					
					this.display.tableOfContents.chapterArea.addElement(label);
				}
			}
			
			private function pageViewReady(e: Event): void {
				this.setFirstPage();
			}
			
			private function setFirstPage(): void{
				this.page = this.book.getFirstPage( this.pageResolution );
				this.display.pageView.source = this.page.image;
				
				this.display.pageView.height = this.page.height;
				this.display.pageView.width = this.display.FullWidth;				
				
				this.adjustPage();
				
				// page items
				this.bindPageItems();
			}
			
			private function nextPage(e: Event): void {				
				var nextPageNumber: Number = this.page.pageNo + 1;
				this.page = this.book.getPage( nextPageNumber, this.pageResolution );
				
				this.display.pageView.source = this.page.image;
				
				this.display.pageView.height = this.page.height;
				this.display.pageView.width = this.display.FullWidth;
				
				this.adjustPage();
				
				// page items
				this.bindPageItems();
			}
			
			private function previousPage(e: Event): void {
				var previousPageNumber: Number = this.page.pageNo - 1;
				this.page = this.book.getPage( previousPageNumber, this.pageResolution );
				this.display.pageView.source = this.page.image;
				
				this.display.pageView.height = this.page.height;
				this.display.pageView.width = this.display.FullWidth;
				
				this.adjustPage();
				
				// page items
				this.bindPageItems();
			}
			
			private function adjustPage(): void {
				var f: Number = ( this.display.FullHeight - this.display.pageView.height );
				var y: Number = (f < 0) ? 0 : (f / 2);
				
				this.pageViewY = y;
				this.display.pageView.y = this.pageViewY;
			}
			
			private function bindPageItems(): void {
				// bind page items
				this.display.addItems( this.page.items );
			}
			
			private function bookChapters(e: Event): void {
				this.display.tableOfContents._parent = this.display;
				this.display.tableOfContents.mainHeight = this.display.FullHeight;
				this.display.tableOfContents.mainWidth = this.display.FullWidth;
				
				this.display.tableOfContents.y = this.display.page.verticalScrollPosition;
				
				this.display.tableOfContents.visible = true;
			}
			
			private function chapterClicked(e: MouseEvent): void {
				var label: Label = e.target as Label;
				
				var children: Number = this.display.tableOfContents.chapterArea.numChildren;
				for(var i: Number = 0; i < children; i++) {
					var _label:Label = this.display.tableOfContents.chapterArea.getElementAt(i) as Label;
					_label.setStyle("backgroundColor", "#1B2630");
				}
				
				label.setStyle("backgroundColor", "#B8BBD9");
				
				var id: Number = label.id.split('-')[1];
				
				this.setChapterPages(id);
			}
			
			private function setChapterPages(chapterId: Number): void {
				var arr: Array = this.book.getChapterPages(chapterId);
				
				this.display.tableOfContents.thumbnailArea.removeAllElements();
				
				for each(var page: Object in arr) {
					var border: BorderContainer = new BorderContainer();
					
					var img: Image = new Image();
					
					img.source = page.image;
					
					img.height = 210;
					img.width = 150;
					
					img.setStyle("paddingTop", 2);
					img.setStyle("paddingBottom", 2);
					
					img.id = "chapter-page-" + page.pageNo;
					
					img.addEventListener(MouseEvent.CLICK, this.chapterPageClicked);
					
					border.addElement(img);
					
					var label: Label = new Label();
					
					label.x = img.width - 40;
					label.y = img.height - 20;
					label.height = 20;
					label.width = 40;
					label.text = page.pageNo;
					
					label.setStyle("backgroundColor", "#1b2630");
					label.setStyle("color", "#FFFFFF");
					label.setStyle("paddingLeft", 5);
					label.setStyle("paddingTop", 5);
					label.setStyle("textAlign", "center");
					
					border.addElement(label);
					
					this.display.tableOfContents.thumbnailArea.addElement(border);
				}
				
			}
			
			private function chapterPageClicked(e: MouseEvent): void {
				var image: Image = e.target as Image;
				var imageSkin: ImageSkin = e.target as ImageSkin;
				
				if(image == null){
					image = imageSkin.parent as Image;
				}
				
				var pageNo: Number = image.id.split('-')[2];
				this.page = this.book.getPage(pageNo, this.pageResolution);
				this.display.pageView.source = this.page.image;
				
				this.display.tableOfContents.visible = false;
				
				this.bindPageItems();
			}

			private function fillLibrary(ev:Event): void {
				for each(var book: Book in this.books) {
					var bookView: BookView = new BookView();
					bookView.book = book;
					
					this.display.booksArea.addElement(bookView);
				}
			}
			
			public function backToLibrary(e: Event): void {
				this.display.mainStack.selectedIndex = DisplayStackConstants.LIBRARY_VIEW_INDEX;
			}
			
			public var cropView: CropView;
			public function initializeCropView(e: Event): void {
				this.addEventListener("crop-view-ready", this.cropViewReady);
				
				this.pageScale();
				
				this.cropView = new CropView();
				
				this.cropView._width = this.display.pageView.sourceWidth;
				this.cropView._height = this.display.pageView.sourceHeight;
				
				this.cropView.x = 
					Math.ceil((this.display.pageView.width - this.display.pageView.sourceWidth) / 2);
				
				this.addElement(this.cropView);
			}
			
			public function cropViewReady(e: Event): void {
				this.cropView.cropCanvasElement.setStyle("backgroundColor", "#FFFFFF");
				this.cropView.cropCanvasElement.setStyle("backgroundAlpha", "0");
				
				this.addEventListener(CropRectangleEvent.CROP_FINISHED, this.cropFinished);
			}
			
			
			public var drawingView: DrawingView;
			public var croppedImage: BitmapData;
			public function cropFinished(e: CropRectangleEvent): void {
				this.removeElement(this.cropView);
				
				// crop image
				var pageScrollPositionX: Number = this.display.page.horizontalScrollPosition;
				var pageScrollPositionY: Number = this.display.page.verticalScrollPosition + this.pageViewY;
				
				var sourceRect: Rectangle = 
					new Rectangle(e.pointStart.x + pageScrollPositionX, e.pointStart.y + pageScrollPositionY, e.pointEnd.x - e.pointStart.x, e.pointEnd.y - e.pointStart.y);
				this.croppedImage = new BitmapData(sourceRect.width, sourceRect.height, true);
				
				this.croppedImage.copyPixels(this.display.pageView.bitmapData, sourceRect, new Point(0, 0));
				
				this.addEventListener("drawing-view-ready", this.drawingViewReady);
				
				this.drawingView = new DrawingView();
				
				this.drawingView.edit = false;
				
				this.drawingView._width = this.display.FullWidth;
				this.drawingView._height = this.display.FullHeight;
				
				this.drawingView.croppedImageWidth = sourceRect.width;
				this.drawingView.croppedImageHeight = sourceRect.height;
				
				this.addElement(this.drawingView);
			}
			
			public function drawingViewReady(e: Event): void {
				this.addEventListener("drawing-view-close", this.drawingViewClose);
				this.drawingView.croppedImage.source = this.croppedImage;
			}
			
			public function drawingViewClose(e: Event): void {
				this.removeElement(this.drawingView);
			}
			
			public function shareDrawing(e: DrawingSaveEvent): void {
				var bitmapDataCopy: BitmapData = e.bitmapData.clone();
				
				if ( this.page.saveDrawing(e.bitmapData) ){
					
					var gonulGozu: GonulGozu = new GonulGozu();
					
					gonulGozu.addEventListener(UploadingComplete.COMPLETE, this.uploadingComplete);
					gonulGozu.addEventListener(UploadingComplete.IOERROR, this.uploadingIOError);
					
					gonulGozu.container = this.drawingView;
					gonulGozu.sendImage( this.page.bookID, this.page.pageNo, e.bitmapData );
					
				} else {
					Alert.show("hata oldu!");
				}			
				
			}
			
			public function saveDrawing(e: DrawingSaveEvent): void {
				if (this.page.saveDrawing(e.bitmapData) ){
					this.drawingView.cursorManager.removeCursor( this.drawingView.cursorID );
					this.removeElement( this.drawingView );	
				} else {
					Alert.show("hata oldu!");
				}
			}
			
			private function uploadingComplete(e: UploadingComplete): void {
				if( e.success ) {
					Alert.show("paylaşıldı");
					//this.drawingView.cursorManager.removeCursor( this.drawingView.cursorID );
					//this.removeElement( this.drawingView );	
				} else {
					Alert.show("Kaydedildi ama paylaşılamadı!");
				}
			}
			
			private function uploadingIOError(e: UploadingComplete): void {
				Alert.show("İnternet bağlantısı yok!");
			}
			
			public function updateDrawing(e: DrawingSaveEvent): void {	
				if( this.page.updateDrawing(e.bitmapData, this.drawingView.editName) ){
					this.drawingView.cursorManager.removeCursor( this.drawingView.cursorID );
					this.removeElement( this.drawingView );
				} else {
					Alert.show("hata oldu!");
				}
			}

			public var drawingsView: DrawingsView;
			public function showDrawingsMenu(e: Event): void {
				this.addEventListener("drawings view ready", this.drawingsViewReady);
				
				this.drawingsView = new DrawingsView();
				
				this.drawingsView._width = this.display.FullWidth;
				this.drawingsView._height = this.display.FullHeight;
				
				this.addElement(this.drawingsView);
			}
			
			public function drawingsViewReady(e: Event): void {
				this.addEventListener("drawings view close", this.drawingsViewClose);
				this.addEventListener(DrawingsClickEvent.DRAWINGS_CLICK, this.drawingsClick);
				// bind chapters
				if( !this.chapters.length ) return;
				
				var label: Label = this.getDrawingsChapterLabel("all", "Hepsi");
				
				this.drawingsView.chapters.addElement(label);
				
				for each( var obj: Object in this.chapters ){
					label = this.getDrawingsChapterLabel("chapter_" + obj.ID, obj.title);
					this.drawingsView.chapters.addElement(label);
				}
			}
			
			public function drawingsViewClose(e: Event): void {
				this.removeElement(this.drawingsView);				
			}
			
			private function getDrawingsChapterLabel(id: String, text: String): Label {
				var label: Label = new spark.components.Label();
				
				label.id = id;
				label.text = text.length > 20 ? text.substr(0, 20) + " ..." : text;
				label.width = this.drawingsView.chapters.width;
				label.height = 30;
				label.setStyle("fontSize", 20);
				label.setStyle("textAlign", "left");
				label.setStyle("color", "#FFFFFF");
				label.setStyle("paddingTop", 10);
				label.setStyle("paddingLeft", 20);
				
				label.addEventListener(MouseEvent.CLICK, this.drawingsView.onChapterClick);
				
				label.width = this.drawingsView.chapters.width;
				
				return label;
			}
			
			public function zoomInClick(e: Event): void {
				this.pageScale(0.1, 0.1);
				this.pageCenter();
			}
			
			public function zoomOutClick(e: Event): void {
				this.pageScale(-0.1, -0.1);
				this.pageCenter();
			}
			
			public function pageScale(x: Number = 0, y: Number = 0): void {
				if(x == 0 && y == 0){
					this.display.pageView.scaleX = 1.0;
					this.display.pageView.scaleY = 1.0;
				}else{
					this.display.pageView.scaleX += x;
					this.display.pageView.scaleY += y;
				}
				this.pageCenter();
			}
			
			public function pageCenter(): void {
				var xCoordinate:Number = (this.display.page.width - this.display.pageView.width * this.display.pageView.scaleX)/2;
				var yCoordinate:Number = (this.display.page.height - this.display.pageView.height * this.display.pageView.scaleY)/2;
				this.display.pageView.y = 0;
				this.display.pageView.x = 0;
			}
			
			public function drawingsClick(e: DrawingsClickEvent): void {
				
				var strArr: Array = e.drawingInfo.split('_');
				var pageNo: Number = int(strArr[1]);
				var drawingNo: Number = int(strArr[2]);
				
				this.drawingView = new DrawingView();
				
				this.drawingView.edit = true;
				this.drawingView.editName = drawingNo.toString();
				
				this.drawingView._width = this.display.FullWidth;
				this.drawingView._height = this.display.FullHeight;
				
				this.addEventListener("bitmap read", this.drawingBitmapDataReady);
				
				var _page: Page = this.book.pages[pageNo - 1];
				_page.getDrawing( drawingNo, this.display.FullWidth, this.display.FullHeight );
				
				this.addEventListener("drawing-view-close", this.drawingViewClose);
			}
			
			public function drawingBitmapDataReady(e: Event): void {
				this.drawingView.setBitmapData( this.page.getDrawingBitmap() );
			
				this.addElement(this.drawingView);
			}
			
			public function itemViewClose(e: Event): void {
				this.display.closeItem();
			}
			
			public var syncView: SyncView;
			public function openSyncView(e: Event): void {
				this.syncView = new SyncView();
				this.syncView.booksInDisk = this.books;
				this.syncView._width = this.display.FullWidth;
				this.syncView._height = this.display.FullHeight;
				
				this.display.addElement( this.syncView );
			}
			
			public function closeSyncView(e: Event): void {
				this.display.removeElement( this.syncView );
			}
			
			private function errorCropView(e: Event): void {
				Alert.show("hata oldu.");
				this.removeElement( this.cropView );
			}
		]]>
		
		
		
	</fx:Script>

</s:WindowedApplication>
