<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="{this._width}" height="{this._height}" creationComplete="onCreationComplete(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import Events.DrawingSaveEvent;
			
			import components.CursorBitmap;
			
			import flash.filesystem.*;
			
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.graphics.*;
			import mx.graphics.codec.PNGEncoder;
			import mx.managers.CursorManager;
			
			import spark.primitives.Rect;
			import spark.skins.spark.ImageSkin;
			
			[Bindable]public var _width: Number 				= 400;
			[Bindable]public var _height: Number 				= 300;
			
			[Bindable]public var croppedImageWidth: Number 		= 0;
			[Bindable]public var croppedImageHeight: Number 	= 0;
			
			public var bitmaps: Vector.<BitmapData> = new Vector.<BitmapData>();
			
			public var color: Number = 0x000000;
			public var thickness: Number = 3;
			public var pen: Number = 1;
			
			public var point: Point;
			public var erase: Boolean = false;
			
			private var goErase: Boolean = false;
			public var edit: Boolean = false;
			public var editName: String;
			
			private var loaderPen: Loader = new Loader();
			private var loaderEraser: Loader = new Loader();
			
			public var penBitmapData: BitmapData;
			public var eraserBitmapData: BitmapData;
			
			public var cursorID: Number = 0;
			
			public function onCreationComplete(e: Event): void {
				
				// pen and eraser icons
				var eraserBytes: ByteArray = this.getEraserBytes();
				loaderEraser.contentLoaderInfo.addEventListener(Event.COMPLETE, this.eraserBytesLoaded);
				loaderEraser.loadBytes( eraserBytes );
				
				var bytes: ByteArray = this.getPenBytes( );
				loaderPen.contentLoaderInfo.addEventListener(Event.COMPLETE, this.penBytesLoaded);
				loaderPen.loadBytes(bytes);
				
				
			}
			
			private function penBytesLoaded(e: Event): void {
				var bytes: * = loaderPen.content;
				
				this.penBitmapData = new BitmapData(bytes.width, bytes.height, true, 0);
				var matrix: Matrix = new Matrix();
				this.penBitmapData.draw(bytes, matrix, null, null, null, true);
				
				CursorBitmap.bitmapData = this.penBitmapData;
				this.cursorID = this.cursorManager.setCursor( CursorBitmap );
				
				this.cursorManager.removeCursor( this.cursorID );
				CursorBitmap.bitmapData = this.eraserBitmapData;
				this.cursorID = this.cursorManager.setCursor( CursorBitmap );
				
				this.cursorManager.removeCursor( this.cursorID );
				CursorBitmap.bitmapData = this.penBitmapData;
				this.cursorID = this.cursorManager.setCursor( CursorBitmap );
				this.cursorManager.currentCursorXOffset = 0;
				this.cursorManager.currentCursorYOffset = -22;
				
				FlexGlobals.topLevelApplication.dispatchEvent(new Event("drawing-view-ready"));
			}
			
			private function eraserBytesLoaded(e: Event): void {
				var bytes: * = loaderEraser.content;
				
				this.eraserBitmapData = new BitmapData(bytes.width, bytes.height, true, 0);
				var matrix: Matrix = new Matrix();
				this.eraserBitmapData.draw(bytes, matrix, null, null, null, true);
			}
			
			private function getPenBytes(): ByteArray {
				var bytes: ByteArray = new ByteArray();
				var file: File = File.applicationDirectory.resolvePath("Assets\\penCursor.png");
				var fileStream: FileStream = new FileStream();
				
				fileStream.open( file, FileMode.READ );
				fileStream.readBytes( bytes );
				fileStream.close();
				
				return bytes;
			}
			
			private function getEraserBytes(): ByteArray {
				var bytes: ByteArray = new ByteArray();
				var file: File = File.applicationDirectory.resolvePath("Assets\\eraserCursor.png");
				var fileStream: FileStream = new FileStream();
				
				fileStream.open( file, FileMode.READ );
				fileStream.readBytes( bytes );
				fileStream.close();
				
				return bytes;
			}
			
			public function setBitmapData(bitmapData: BitmapData): void {
				
				this.bitmaps.push( bitmapData );
				
				this.transparentArea.graphics.beginBitmapFill(bitmapData, new Matrix());
				this.transparentArea.graphics.drawRect(0, 0, bitmapData.width, bitmapData.height);
				this.transparentArea.graphics.endFill();
			}
			
			public function onCloseClick(e: MouseEvent): void {
				
				this.cursorManager.removeCursor( this.cursorID );
				
				FlexGlobals.topLevelApplication.dispatchEvent(new Event("drawing-view-close"));
			}
			
			public function onSaveClick(e: MouseEvent): void {
				var bitmapData: BitmapData = new BitmapData(this.transparentArea.width, this.transparentArea.height, true, 0);
				bitmapData.draw(this.transparentArea);
				
				if( this.croppedImage.bitmapData ){
					bitmapData.copyPixels(this.croppedImage.bitmapData, new Rectangle(0, 0, this.croppedImage.width, this.croppedImage.height), new Point(10, 10));
				}
				
				if( this.edit ){
					FlexGlobals.topLevelApplication.dispatchEvent(new DrawingSaveEvent(DrawingSaveEvent.DRAWING_UPDATE, bitmapData))	
				}else{
					FlexGlobals.topLevelApplication.dispatchEvent(new DrawingSaveEvent(DrawingSaveEvent.DRAWING_SAVE, bitmapData))	
				}
			}
			
			public function onShareClick(e: MouseEvent): void {
				Alert.show("share click");
			}
			
			public function onMouseDown(e: MouseEvent): void {
				this.point = new Point(this.contentMouseX, this.contentMouseY);
	
				// save bitmapdata for undo
				var bitmap: BitmapData = new BitmapData(this.transparentArea.width, this.transparentArea.height, true, 0x000000);
				bitmap.draw(this.transparentArea);
				this.bitmaps.push(bitmap);
				
				if( !this.erase ) {
					this.transparentArea.graphics.lineStyle(this.thickness, this.color, this.pen, true);
					this.transparentArea.graphics.drawCircle(this.point.x, this.point.y, this.thickness / 5);
				}
			}
			
			public function onMouseUp(e: MouseEvent): void {
				
			}
			
			public function onMouseMove(e: MouseEvent): void {
				if(e.buttonDown){
					if( this.erase ) {
						this.transparentArea.graphics.lineStyle(this.thickness * 4, 0xd6d8d8, this.pen);
						this.transparentArea.graphics.moveTo(this.point.x, this.point.y);
						this.point.x = this.contentMouseX;
						this.point.y = this.contentMouseY;
						this.transparentArea.graphics.lineTo(this.point.x, this.point.y);
					}else{
						this.transparentArea.graphics.lineStyle(this.thickness, this.color, this.pen, true, "none", "round", "round", 1);
						this.transparentArea.graphics.moveTo(this.point.x, this.point.y);
						
						this.point.x = this.contentMouseX;
						this.point.y = this.contentMouseY;
						this.transparentArea.graphics.lineTo(this.point.x, this.point.y);
					}
				}
			}
			
			public function onEraseClick(e: MouseEvent): void {
				
				if( this.erase ) {
					
					CursorBitmap.bitmapData = this.penBitmapData;
					
					this.cursorManager.removeCursor( this.cursorID );
					
					this.cursorID = this.cursorManager.setCursor( CursorBitmap );
					
					this.cursorManager.currentCursorXOffset = 0;
					this.cursorManager.currentCursorYOffset = -22;
					
					this.eraserButton.source = "Assets\\eraser.png";
					
					this.erase = false;
				}else{

					CursorBitmap.bitmapData = this.eraserBitmapData;
					
					this.cursorManager.removeCursor( this.cursorID );
					
					this.cursorID = this.cursorManager.setCursor( CursorBitmap  );
					this.cursorManager.currentCursorXOffset = -12;
					this.cursorManager.currentCursorYOffset = -15;
					
					this.eraserButton.source = "Assets\\pen.png";
					
					this.erase = true;
				}
				
			}
			
			public function onUndoClick(e: MouseEvent): void {				
				this.transparentArea.graphics.clear();
				if( this.bitmaps.length ){
					var bitmap: BitmapData = this.bitmaps.pop();
					this.transparentArea.graphics.beginBitmapFill(bitmap, new Matrix());
					this.transparentArea.graphics.drawRect(0, 0, bitmap.width, bitmap.height);
					this.transparentArea.graphics.endFill();
				}
			}
			
			public function onRefreshClick(e: MouseEvent): void {
				this.transparentArea.graphics.clear();
			}
			
			public function onColorClick(e: MouseEvent): void {
				var image:Image = e.target as Image;
				var imageSkin: ImageSkin = e.target as ImageSkin;
				if(image == null){
					image = imageSkin.parent as Image;
				}
				var color: String = image.id.split('_')[1];
				
				switch( color ) {
					case "black":
						this.color = 0x000000;
						break;
					case "yellow":
						this.color = 0XFFFF00;
						break;
					case "red":
						this.color = 0xFF0000;
						break;
					case "green":
						this.color = 0x00FF00;
						break;
					case "blue":
						this.color = 0X0000FF;
						break;
					default:
						this.color = 0x000000;
				}
			}
			
			public function onThicknessClick(e: MouseEvent): void {
				var image: Image = e.target as Image;
				var imageSkin: ImageSkin = e.target as ImageSkin;
				if(image == null){
					image = imageSkin.parent as Image;
				}
				var thickness: String = image.id.split('_')[1];
				
				this.thickness = new Number(thickness);
			}
			
		]]>
	</fx:Script>
	
	<s:BorderContainer width="{this._width}" height="{this._height - this.drawingViewBottom.height}" x="0" y="0" backgroundColor="#d6d8d8">
		<mx:Canvas id="drawingViewDrawingArea"
			width="{this._width}" height="{this._height - this.drawingViewBottom.height}" 
			x="0" y="0">
		</mx:Canvas>
		
		<s:Image id="croppedImage" x="10" y="10" width="{this.croppedImageWidth}" height="{this.croppedImageHeight}">
			
		</s:Image>
	</s:BorderContainer>
	
	<!-- transparent drawing area -->
	<mx:Canvas id="transparentArea" visible="true"
			   width="{this._width}" height="{this._height - this.drawingViewBottom.height}" 
			   x="0" y="0" 
			   backgroundColor="#000000" backgroundAlpha="0" mouseDown="onMouseDown(event)" mouseMove="onMouseMove(event)" mouseUp="onMouseUp(event)">
	</mx:Canvas>
	
	<s:BorderContainer textAlign="center" width="{this._width}" id="drawingViewBottom" height="37" x="0" y="{this.drawingViewDrawingArea.height}" backgroundColor="#1b2630">			
		<!-- close button -->
		<s:Image id="backbutton" x="5" top="2" width="101" height="31" source="/Assets/backbutton.png" click="onCloseClick(event)">
			
		</s:Image>
		<!-- save button -->
		<s:Image x="111" width="101" top="2" height="31" source="/Assets/savebutton.png" click="onSaveClick(event)">
			
		</s:Image>
		<!-- share button -->
		<s:Image  x="217" width="101" top="2" height="31" source="/Assets/sharebutton.png" click="onShareClick(event)" visible="false">
			
		</s:Image>
		
		<!-- erase button -->
		<s:Image id="eraserButton" x="{this._width - 666}" width="71" height="31" top="2" source="/Assets/eraser.png" click="onEraseClick(event)">
			
		</s:Image>
		<!-- undo button -->
		<s:Image x="{this._width - 588}" width="71" height="31" top="2" source="/Assets/undo.png" click="onUndoClick(event)">
			
		</s:Image>
		
		<!-- refresh button -->
		<s:Image x="{this._width - 510}" width="71" height="31" top="2" source="/Assets/refresh.png" click="onRefreshClick(event)">
			
		</s:Image>
		
		<!-- color buttons -->
		<s:Image id="color_black" x="{this._width - 402}" top="2" width="31" height="31" source="/Assets/colorbutton-1.png" click="onColorClick(event)">
			
		</s:Image>
		<s:Image id="color_yellow" x="{this._width - 364}" top="2" width="31" height="31" source="/Assets/colorbutton-2.png" click="onColorClick(event)">
			
		</s:Image>
		<s:Image id="color_red" x="{this._width - 326}" top="2" width="31" height="31" source="/Assets/colorbutton-3.png" click="onColorClick(event)">
			
		</s:Image>
		<s:Image id="color_green" x="{this._width - 288}" top="2" width="31" height="31" source="/Assets/colorbutton-4.png" click="onColorClick(event)">
			
		</s:Image>
		<s:Image id="color_blue" x="{this._width - 250}" top="2" width="31" height="31" source="/Assets/colorbutton-5.png" click="onColorClick(event)">
			
		</s:Image>
		
		<!-- thickness buttons -->
		<s:Image id="thickness_10" x="{this._width - 188}" top="2" width="31" height="31" source="/Assets/thicknessbutton-5.png" click="onThicknessClick(event)">
			
		</s:Image>
		<s:Image id="thickness_8" x="{this._width - 150}" top="2" width="31" height="31" source="/Assets/thicknessbutton-4.png" click="onThicknessClick(event)">
			
		</s:Image>
		<s:Image id="thickness_6" x="{this._width - 114}" top="2" width="31" height="31" source="/Assets/thicknessbutton-3.png" click="onThicknessClick(event)">
			
		</s:Image>
		<s:Image id="thickness_4" x="{this._width - 76}" top="2" width="31" height="31" source="/Assets/thicknessbutton-2.png" click="onThicknessClick(event)">
			
		</s:Image>
		<s:Image id="thickness_2" x="{this._width - 38}" top="2" width="31" height="31" source="/Assets/thicknessbutton-1.png" click="onThicknessClick(event)">
			
		</s:Image> 
	</s:BorderContainer>
</s:Group>
